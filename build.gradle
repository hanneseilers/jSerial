plugins {
    id 'java-library'
}

group = 'de.hanneseilers.jserial'
version = '0.0.5'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-core:2.25.1'
    implementation 'org.apache.logging.log4j:log4j-api:2.25.1'

    implementation fileTree("libs") { include "*.jar" }
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

// task building fat jar with all dependencies
tasks.register('buildLib', Jar) {
    manifest {
        attributes 'Main-Class': 'com.baeldung.fatjar.Application'
    }
    destinationDirectory.set(layout.projectDirectory.asFile)
    archiveBaseName = 'jSerial'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

// task zipping lib-sources
tasks.register('zipLibSources', Zip) {
    description = 'Packs lib-sources directory into a versioned zip at project root'
    from('lib-sources') { into('lib-sources') }
    archiveBaseName.set('lib-sources')
    archiveVersion.set(project.version.toString())
    destinationDirectory.set(layout.projectDirectory.asFile)
}

// task build fat jar and lib-sources zip
tasks.register('releaseBundle') {
    group = 'build'
    description = 'Build fat JAR and lib-sources zip into project root'
    dependsOn(tasks.buildLib, tasks.zipLibSources)
}